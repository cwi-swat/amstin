schema SchemaSchema

  primitive \str
  primitive \int
  primitive \bool
  primitive \real

  class Schema
    #name: str
    classes: Klass* = "@types.select(&:Klass?)"
    primitives: Primitive* = "@types.select(&:Primitive?)"
    types: Type*
  end

  class Type
    #name: str
    \schema: Schema / Schema.types
  end
  
  class Primitive < Type
  end
  
  class Klass < Type
    super: Klass?
    subtypes: Klass* / Klass.super
    defined_fields: Field*
    fields: Field* = "@super ? @defined_fields + @super.fields : @defined_fields"
  end
  
  class Field
    #name: str
    #owner: Klass / Klass.defined_fields
    type: Type
    optional: bool
    many: bool
    key: bool
    inverse: Field? / Field.inverse 
    computed: str?
  end
end
