  grammar GrammarGrammar
  start Grammar
  
  Grammar ::= [Grammar] "grammar" name:sym "start" \start:Rule^

  Rule ::= [Rule] name:key "::=" arg:Alt

  Alt ::= [Alt] alts:{Create "|"}+

  Create ::= [Create] "[" name:sym "]" arg:Sequence
  	   | Sequence

  Sequence ::= [Sequence] elements:Field*


  Field ::= [Field] name:sym ":" arg:Pattern
   	  | Pattern
  
  Pattern ::= 
            [Value] kind:"int"				
          | [Value] kind:"str"
          | [Value] kind:"sqstr"
          | [Value] kind:"real"
          | [Value] kind:"bool"
          | [Value] kind:"sym"
	  | [Key] "key"
          | [Ref] name:sym "^"
          | [Lit] value:str				
          | [Lit] value:sqstr				
          | [Call] rule:Rule^
          | [Regular] arg:Pattern "?"			
          | [Regular] arg:Pattern "+"  			
          | [Regular] arg:Pattern "*"  			
          | [Regular] "{" arg:Pattern sep:str "}" "+" 	
          | [Regular] "{" arg:Pattern sep:str "}" "*" 	
	  | "(" Alts ")"

